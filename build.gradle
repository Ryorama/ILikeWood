buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0.16,6.2)'
}

apply plugin: 'org.spongepowered.mixin'

mixin {
    add sourceSets.main, "ilikewood.mixins.refmap.json"
}

version = "${version_mc}-${version_major}.${version_api}.${version_minor}.${version_patch}"
group = "yamahari.${mod_id}"
archivesBaseName = "${mod_id}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    main {
        resources {
            srcDirs = ['src/main/resources', 'src/data/resources/data', 'src/data/resources/textures']
        }
    }

    api {
    }
}

configurations {
    apiImplementation.extendsFrom(minecraft)
}

minecraft {
    mappings channel: 'official', version: "${version_mc}"

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "--mixin=ilikewood.mixins.json"
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            arg "--mixin=ilikewood.mixins.json"
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        data {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'debug'
            property "${mod_id}.datagen.modid", "${mod_id}"
            arg "--mixin=ilikewood.mixins.json"
            args '--mod', "${mod_id}",
                    '--all',
                    '--output', file("src/data/resources/dummy"),
                    '--existing', file('src/main/resources/')
            mods {
                ilikewood {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }
    }
}

repositories {
//    maven {
//        // location of the maven that hosts JEI files
//        name = "Progwml6 maven"
//        url = "https://dvs1.progwml6.com/files/maven/"
//    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${version_mc}-${version_forge}"

    compileOnly fg.deobf("mezz.jei:jei-${version_mc}-common-api:${version_jei}")
    compileOnly fg.deobf("mezz.jei:jei-${version_mc}-forge-api:${version_jei}")
    runtimeOnly fg.deobf("mezz.jei:jei-${version_mc}-forge:${version_jei}")

    annotationProcessor 'org.spongepowered:mixin:0.8.+:processor'

    implementation sourceSets.api.output

    //runtimeOnly project(path: ':config', configuration: 'configJars')
    //runtimeOnly project(path: ':config', configuration: 'configPanelsOnly')
    //runtimeOnly project(path: ':config', configuration: 'configPanelsChain')
    //runtimeOnly project(path: ':config', configuration: 'configPanelsStairs')
}


def replaceVersions = tasks.register('replaceVersions', Copy) {
    it.outputs.upToDateWhen { false }
    it.from(sourceSets.main.resources) {
        include 'META-INF/mods.toml'
        expand 'mod_id': mod_id,
                'version': version,
                'version_range_mc': version_range_mc,
                'version_range_loader': version_range_loader,
                'version_range_forge': version_range_forge
    }
    it.into "$buildDir/resources/main/"
}


processResources {
    exclude 'META-INF/mods.toml'
    configure {
        finalizedBy replaceVersions
    }
}

classes.configure {
    dependsOn(replaceVersions)
}

jar {
    from sourceSets.main.output.classesDirs
    from sourceSets.api.output.classesDirs
    from sourceSets.main.output.resourcesDir
    from sourceSets.api.output.resourcesDir

    sourceSets.main.resources {
        exclude '**/.cache'
    }

    manifest.attributes([
            "Specification-Title"     : "${mod_id}",
            "Specification-Vendor"    : "yamahari",
            "Specification-Version"   : "${project.version}",
            "Implementation-Title"    : "${mod_id}",
            "Implementation-Version"  : "${project.version}",
            "Implementation-Vendor"   : "yamahari",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Automatic-Module-Name"   : "${mod_id}",
            "MixinConfigs": "ilikewood.mixins.json"
    ])

    finalizedBy 'reobfJar'
}

task apiJar(type: Jar, dependsOn: apiClasses) {
    archiveClassifier.set('api')
    from sourceSets.api.output
    manifest.attributes([
            "Specification-Title"     : "${mod_id}-api",
            "Specification-Vendor"    : "yamahari",
            "Specification-Version"   : "${project.version}",
            "Implementation-Title"    : "${mod_id}-api",
            "Implementation-Version"  : "${project.version}",
            "Implementation-Vendor"   : "yamahari",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "Automatic-Module-Name"   : "${mod_id}-api"
    ])
    afterEvaluate { finalizedBy reobfApiJar }
}

reobf {
    apiJar { classpath.from(sourceSets.api.compileClasspath) }
    jar { classpath.from(sourceSets.main.compileClasspath) }
}

task reobf {
    dependsOn reobfJar
    dependsOn reobfApiJar
}